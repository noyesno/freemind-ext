<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">

<xsl:output method="html" doctype-public="-//W3C//DTD XHTML 1.0 Strict//EN"
    doctype-system="http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd"/>
<xsl:param name="theme">style/faq</xsl:param>

<xsl:template match="/">
<!-- !DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd"-->
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
  <xsl:choose>
    <xsl:when test="/map/node">
      <title><xsl:value-of select="/map/node/@TEXT"/></title>
    </xsl:when>
    <xsl:otherwise>
      <title>FreeMind 2 FAQ</title>
    </xsl:otherwise>
  </xsl:choose>
<meta name="version" content="mm2faq 0.1" />

<xsl:element name="link">
  <xsl:attribute name="rel">stylesheet</xsl:attribute>
  <xsl:attribute name="href"><xsl:value-of select="$theme"/>/faq.css</xsl:attribute>
  <xsl:attribute name="type">text/css</xsl:attribute>
</xsl:element>
</head>
<body>

<h1><xsl:value-of select="/map/node/@TEXT"/></h1>
<!-- TODO: Title List -->
<ol>
<xsl:for-each select="/map/node/node">
  <xsl:choose>
      <xsl:when test="hook">
        <li><a href="#{generate-id()}"><xsl:value-of select="@TEXT"/></a></li>
      </xsl:when>
      <xsl:otherwise>
        <li><xsl:value-of select="@TEXT"/></li>
        <ol>
        <xsl:for-each select="node">
        <li><a href="#{generate-id()}"><xsl:value-of select="@TEXT"/></a></li>
        </xsl:for-each>
        </ol>
      </xsl:otherwise>
  </xsl:choose>
</xsl:for-each>
</ol>

<hr/>

<xsl:apply-templates select="/map/node/node" mode="content"/>

<hr/>
<div id="footer">
Generated by mm2faq for FreeMind ...
</div>
</body>
</html>
</xsl:template>

<xsl:template match="/map/node/node" mode="content">
  <xsl:choose>
      <xsl:when test="hook">
        <xsl:apply-templates select="." mode="question"/>
      </xsl:when>
      <xsl:otherwise>
        <h2 class="section"><xsl:value-of select="@TEXT"/></h2>
        <xsl:apply-templates select="node" mode="question"/>
      </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="node" mode="question">
  <a name="{generate-id()}"/>
  <h3><xsl:value-of select="@TEXT" /></h3>
  <div class="answer">
    <xsl:apply-templates select="hook" mode="answer"/>
    <xsl:choose>
      <xsl:when test="node"><dl><xsl:apply-templates select="node" mode="content"/></dl></xsl:when>
    </xsl:choose>
  </div>
</xsl:template>

<xsl:template match="node" mode="content">
  <dt><xsl:value-of select="@TEXT" /></dt>
  <dd><xsl:apply-templates select="hook" mode="answer"/></dd>
  <xsl:choose>
    <xsl:when test="node">
    <dd><xsl:apply-templates select="node" mode="content"/></dd>
    </xsl:when>
  </xsl:choose>
</xsl:template>

<xsl:template match="hook[@NAME='accessories/plugins/NodeNote.properties']" mode="answer">
  <xsl:choose>
    <xsl:when test="./text">
	  <xsl:value-of select="./text"/>
    </xsl:when>
  </xsl:choose>
</xsl:template>

</xsl:stylesheet>
